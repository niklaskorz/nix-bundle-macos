#!/usr/bin/env zsh

set -e

cleanup() {
    unset dep_path_old
    unset dep_path_new
}
trap cleanup EXIT INT QUIT TERM

if [ -z "$1" ]; then
	echo "Missing required input PROGRAM_NAME"
	exit;
fi

if [ "$1" = "--help" ]; then
	echo "Usage:"
	echo "nix-bundle-macos PROGRAM_NAME"
	exit;
fi

FLAKE="$1"

NIX_STORE_SOURCE="/nix/store"
if [ ! -d "$NIX_STORE_SOURCE" ]; then
	echo "Missing directory $NIX_STORE_SOURCE"
	exit;
fi

OUT_PATH=$(nix build -j auto --no-link --print-out-paths "$FLAKE")
PROGRAM_NAME=$(nix derivation show "$FLAKE" | jq -r '.[].name')

if [ ! -d "$OUT_PATH" ]; then
	echo "Missing directory for $PROGRAM_NAME"
	exit;
fi

SOURCE_DIR="/dev/null"
TARGET_DIR="/dev/null"

APP_NAME=""

if  [ -d "$OUT_PATH/Applications" ]; then
	CONTENT_COUNT=$(ls -1 "$OUT_PATH/Applications" | wc -l)
	if [ "$CONTENT_COUNT" -ne 1 ]; then
		echo "I don't know how to handle $CONTENT_COUNT items in $OUT_PATH/Applications"
		exit
	fi
	APP_NAME=$(ls -1 "$OUT_PATH/Applications/" | head -n 1)
	SOURCE_DIR="$OUT_PATH/Applications/$APP_NAME"
	TARGET_DIR="$PWD/result/$APP_NAME"
elif [ -d "$OUT_PATH/bin" ]; then
	SOURCE_DIR="$OUT_PATH/bin"
	TARGET_DIR="$PWD/result/$PROGRAM_NAME"
else
	echo "No Application or bin directory found for $PROGRAM_NAME!"
	exit
fi

if [ -d "$TARGET_DIR" ]; then
	while true; do
		read -r "yn?Remove old $TARGET_DIR directory? y/n "
		case $yn in
			[Yy]* ) rm -rf "$TARGET_DIR"; break;;
			[Nn]* ) exit;;
			* ) echo "Please answer yes or no.";;
		esac
	done
fi

mkdir -p result

echo "Copying program..."
cp -R "$SOURCE_DIR" "$TARGET_DIR"
chmod -R +w "$TARGET_DIR"

if [ ! -z "$APP_NAME" ]; then
	NIX_STORE_TARGET="$TARGET_DIR/Contents/nix"
	NIX_STORE_DYLD="@executable_path/../nix"
else
	NIX_STORE_TARGET="$TARGET_DIR/nix"
	NIX_STORE_DYLD="@executable_path/nix"
fi

mkdir -p "$NIX_STORE_TARGET"

deps=$(nix-store -qR "$OUT_PATH")

echo "Copying dependencies..."
for dep_path_old in $(echo "$deps"); do
        dep_path_new=$(echo "$dep_path_old" | sed "s#${NIX_STORE_SOURCE}/[0-9a-z]\{32\}\-#${NIX_STORE_TARGET}/#")
        cp -R "$dep_path_old" "$dep_path_new"
        chmod -R +w "$dep_path_new"
done

find "${NIX_STORE_TARGET}" -mindepth 2 -maxdepth 2 -type d -not -name 'Library' -not -name 'lib' -exec rm -rf "{}" \;

echo "Updating paths to nix store..."
for dep_path_old in $(echo "$deps"); do
        dep_path_new=$(echo "$dep_path_old" | sed "s#${NIX_STORE_SOURCE}/[0-9a-z]\{32\}\-#${NIX_STORE_DYLD}/#")
        export dep_path_old
        export dep_path_new

        # Replace content in text files
        grep -rIl "$NIX_STORE_SOURCE" "$TARGET_DIR" \
                | xargs -n 1 -P 4 sh -c 'sed -i "" -e "s#${dep_path_old}#${dep_path_new}#g" "$1"' sh

        # Replace content in binary files
        grep -rl "$NIX_STORE_SOURCE" "$TARGET_DIR" \
                | xargs -n 1 -P 4 sh -c './patch_strings_in_file "$1" "$dep_path_old" "$dep_path_new"' sh
done

INSTALLER_DIR="/dev/null"

if [ ! -z "$APP_NAME" ]; then
	#INSTALLER_DIR="/Applications/$PROGRAM_NAME-Installer"
	INSTALLER_DIR="$(mktemp -d -t """$PROGRAM_NAME-Installer.XXXXXXXXXX""")"
else
	INSTALLER_DIR="$(mktemp -d -t """$PROGRAM_NAME.XXXXXXXXXX""")/$PROGRAM_NAME.tar.gz"
fi

if [ ! -z "$APP_NAME" ]; then
	if [ -d "$INSTALLER_DIR" ]; then
		while true; do
		    read -r "yn?Remove old $INSTALLER_DIR directory? y/n "
		    case $yn in
			[Yy]* ) rm -rf "$INSTALLER_DIR"; break;;
			[Nn]* ) exit;;
			* ) echo "Please answer yes or no.";;
		    esac
		done
	fi

	#echo 'TODO: how do we sign this? Do we sign both the DMG and the XYZ.app?'
	#echo 'Until the signing is done, it'll be necessary to right-click open.
	codesign --force --deep --sign - "$TARGET_DIR"
	codesign --force --deep --sign - "$NIX_STORE_TARGET"/**/*.dylib

	mkdir "$INSTALLER_DIR"
	cp -R "$TARGET_DIR" "$INSTALLER_DIR"
	ln -s "$PWD/Applications" "$INSTALLER_DIR/Applications"

	echo ""
	echo "$PROGRAM_NAME executable bundle: $TARGET_DIR"
	echo "  bundle for creating DMG: $INSTALLER_DIR"

	echo 'Use Disk Utility "File > New Image > Image from Folder" to create DMG.'
	# TODO: look at using a DMG builder like this:
	# https://github.com/andreyvit/create-dmg
	# But note that project is looking for new maintainers.
else
	codesign --force --deep --sign - "$TARGET_DIR"/*
	codesign --force --deep --sign - "$NIX_STORE_TARGET"/**/*.dylib

	tar -czvf "$INSTALLER_DIR" "$TARGET_DIR"	
	echo ""
	echo "$PROGRAM_NAME executable bundle: $TARGET_DIR"
	echo "  compressed bundle: $INSTALLER_DIR"
	echo "Add $TARGET_DIR to your PATH"
fi
